ZZUTXTM2	;jli/oifo-oak - unit test routine for XTMRPAR1 - continuation ;4/29/04  16:48
	;;7.3;TOOLKIT;**81**;Apr 25, 1995
	D EN^XTMUNIT("ZZUTXTM2")
	Q
	;
EXCEPT	;
	N ARRAY
	S ARRAY(1,1,"COMMENT")="; COMMENT"
	S ARRAY(2,1,"COMMENT")="; ZEXCEPT: XTMCOUNT,XTMVALUE,XTLOG  SET IN XXX KILLED IN XXXX"
	S ARRAY(3,1,"COMMENT")=""
	D EXCEPT^XTMRPAR1(1,3,"ARRAY","EXCEPT")
	D CHKEQ^XTMUNIT($O(EXCEPT("")),"XTLOG","DIDN'T RETURN XTLOG")
	D CHKEQ^XTMUNIT($O(EXCEPT("XTLOG")),"XTMCOUNT","Didn't return XTMCOUNT")
	Q
	;
GETCMD	;
	N LINE,LINENUM,ARGNUM,LOCSTOR
	S LINENUM=1,ARGNUM=0,LOCSTOR="GETCMD"
	K @LOCSTOR
	S LINE=$$GETCMD^XTMRPAR1("D GETCMD",LINENUM,.ARGNUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD")),"DO","DIDN'T GET COMMAND RIGHT")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",1)),"GETCMD","DIDN'T GET ARG RIGHT")
	;+7
	;K @LOCSTOR
	S LINE=$$GETCMD^XTMRPAR1("D GETCMD,GETCMD1",LINENUM,.ARGNUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",1)),"GETCMD","DIDN'T GET ARG RIGHT")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",2)),"GETCMD1","DIDN'T GET SECOND ARG RIGHT")
	;
	;K @LOCSTOR
	S LINE=$$GETCMD^XTMRPAR1("S XVAR1=XVAR2+XVAR3,M=2*5 REST OF LINE",LINENUM,.ARGNUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",1)),"XVAR1=XVAR2+XVAR3","DIDN'T GET ARG RIGHT")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",2)),"M=2*5","DIDN'T GET SECOND ARG RIGHT")
	;
	;K @LOCSTOR
	S LINE=$$GETCMD^XTMRPAR1("S XVAR1=""DATA"",M=$S(X="""":""A"",1:""B"") REST OF LINE",LINENUM,.ARGNUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",1)),"XVAR1=""DATA""","DIDN'T GET ARG RIGHT")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"CMD","ARG",2)),"M=$S(X="""":""A"",1:""B"")","DIDN'T GET SECOND ARG RIGHT")
	;
	S LINE=$$GETCMD^XTMRPAR1(";JLI - NEW TEST ;",LINENUM,.ARGNUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,ARGNUM,"COMMENT")),";JLI - NEW TEST ;","FAILED ON COMMENT")
	;
	;K @LOCSTOR
	Q
	;
NEXTTOKN	;
	; <SPACE> TERMINATED TOKEN
	N TOKEN,TERM
	S LINE=$$NEXTTOKN^XTMRPAR1("THIS IS MY LINE",.TOKEN,.TERM)
	D CHKEQ^XTMUNIT($G(TOKEN),"THIS","TOKEN VALUE NOT EXPECTED TOKEN TERMINATED BY SPACE")
	D CHKEQ^XTMUNIT($G(TERM)," ","TERM VALUE NOT EXPECTED SPACE TERMINATOR")
	D CHKEQ^XTMUNIT(LINE,"IS MY LINE","LINE RETURNED WAS NOT EXPECTED RESULT")
	; <COLON> TERMINATED TOKEN
	K TOKEN,TERM
	S LINE=$$NEXTTOKN^XTMRPAR1("D:N>0 N1^XTVAL",.TOKEN,.TERM)
	D CHKEQ^XTMUNIT($G(TOKEN),"D","TOKEN VALUE NOT EXPECTED TOKEN TERMINATED BY COLON")
	D CHKEQ^XTMUNIT($G(TERM),":","TERM VALUE NOT EXPECTED SPACE TERMINATOR")
	D CHKEQ^XTMUNIT(LINE,"N>0 N1^XTVAL","LINE RETURNED WAS NOT EXPECTED RESULT")
	; " QUOTED TEXT
	K TOKEN,TERM
	S LINE=$$NEXTTOKN^XTMRPAR1("""THIS IS MY TEXT"" AND SO",.TOKEN,.TERM)
	D CHKEQ^XTMUNIT($G(TOKEN),"""THIS IS MY TEXT""","TOKEN VALUE NOT EXPECTED TOKEN IN QUOTES")
	D CHKEQ^XTMUNIT($G(TERM)," ","TERM VALUE NOT EXPECTED SPACE TERMINATOR")
	D CHKEQ^XTMUNIT(LINE,"AND SO","LINE RETURNED WAS NOT EXPECTED RESULT")
	; " QUOTED TEXT WITH EMBEDDED QUOTES
	K TOKEN,TERM
	S LINE=$$NEXTTOKN^XTMRPAR1("""THIS IS """"MY"""" TEXT"" AND SO",.TOKEN,.TERM)
	D CHKEQ^XTMUNIT($G(TOKEN),"""THIS IS """"MY"""" TEXT""","TOKEN VALUE NOT EXPECTED TOKEN IN QUOTES")
	D CHKEQ^XTMUNIT($G(TERM)," ","TERM VALUE NOT EXPECTED SPACE TERMINATOR")
	D CHKEQ^XTMUNIT(LINE,"AND SO","LINE RETURNED WAS NOT EXPECTED RESULT")
	Q
	;
SETTAG	;
	N LINENUM,LOCSTOR
	S LINENUM=1,LOCSTOR="SETTAG" K @LOCSTOR
	S LINE=$$SETTAG^XTMRPAR1("TAG1 REST OF LINE",LINENUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG")),"TAG1","DIDN'T FIND TAG CORRECTLY")
	; +5
	K @LOCSTOR
	S LINE=$$SETTAG^XTMRPAR1("TAG2(VAR1,VAR2) REST OF LINE",LINENUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG")),"TAG2","DIDN'T FIND TAG NAME CORRECTLY")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",1)),"VAR1","DIDN'T GET FIRST ARG CORRECTLY")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",2)),"VAR2","DIDN'T GET SECOND ARG CORRECTLY")
	; +11
	K @LOCSTOR
	S LINE=$$SETTAG^XTMRPAR1("TAG3(VAR1,VAR2(VAR3),VAR4) REST OF LINE",LINENUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",2)),"VAR2(VAR3)","DIDN'T GET SUBSCRIPTED ARG CORRECTLY")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",3)),"VAR4","DIDN'T GET NEXT ARG CORRECTLY")
	; +16
	K @LOCSTOR
	S LINE=$$SETTAG^XTMRPAR1("TAG3(VAR1(VAR2),$NA(^TMP($J,""NEWVALUE"",VAR3)),VAR4) REST OF LINE",LINENUM,LOCSTOR)
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",1)),"VAR1(VAR2)","DIDN'T GET SUBSCRIPTED ARG CORRECTLY")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",2)),"$NA(^TMP($J,""NEWVALUE"",VAR3))","DIDN'T GET SUBSCRIPTED ARG CORRECTLY")
	D CHKEQ^XTMUNIT($G(@LOCSTOR@(LINENUM,0,"TAG","ARG",3)),"VAR4","DIDN'T GET NEXT ARG CORRECTLY")
	; +22
	K @LOCSTOR
	Q
	;
XTROU	;
	;;ZZUTXTM1;primary routine for this set of tests
XTENT	;
	;;NEXTTOKN;get next token
	;;SETTAG;get tag and args
	;;GETCMD;get commands, conditionals, and args
	;;EXCEPT;get exception list from comment
	
